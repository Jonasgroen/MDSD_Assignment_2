grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Model:
	items += MathExp*
;

MathExp:
	'var' name=ID '=' exp=Exp
;

Exp: PlusOrMinus;

PlusOrMinus returns Exp:
	MulOrDiv (
		({PlusOrMinus.left=current} operator=('+'|'-'))
		right=MulOrDiv
	)*
;

MulOrDiv returns Exp:
	Primary (
		{MulOrDiv.left=current} operator=('*'|'/')
		right=Primary
	)*
;

Primary returns Exp:
	 '(' Exp ')' | {Number} value=INT | {VariableUse} ref = [MathExp]
;
